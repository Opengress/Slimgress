import java.text.SimpleDateFormat

plugins {
    id 'com.android.application'
}

static def buildTime() {
    def df = new SimpleDateFormat("yyyy-MM-dd'T'HH:mm'Z'")
    df.setTimeZone(TimeZone.getTimeZone("UTC"))
    return df.format(new Date())
}

android {
    compileSdk 35

    defaultConfig {
        applicationId "net.opengress.slimgress"
        minSdk 21
        targetSdk 35
//        1112223333
        versionCode 10004
        versionName "0.1.4"

        resConfigs "en"
        buildConfigField "String", "BUILD_TIME", "\"${buildTime()}\""

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }
    signingConfigs {
        release {
            storeFile file(RELEASE_STORE_FILE)
            storePassword RELEASE_STORE_PASSWORD
            keyAlias RELEASE_KEY_ALIAS
            keyPassword RELEASE_KEY_PASSWORD

            // Optional, specify signing versions used
            v1SigningEnabled true
            v2SigningEnabled true
        }
    }
    buildTypes {
        release {
            signingConfig signingConfigs.release
        }
        full {
            signingConfig signingConfigs.release
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_11
        targetCompatibility JavaVersion.VERSION_11
    }

    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
        full {
            minifyEnabled false
        }
        debug {
            debuggable true
        }
    }


    namespace 'net.opengress.slimgress'
    buildFeatures {
        buildConfig true
    }
    dependenciesInfo {
        includeInApk false
        includeInBundle false
    }

}

dependencies {
    androidTestImplementation 'junit:junit:4.13.2'
    implementation 'ch.acra:acra-http:5.11.3'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.4'
    implementation 'com.github.bumptech.glide:glide:4.16.0'
    implementation 'com.google.android.material:material:1.12.0'
    implementation 'com.google.guava:guava:33.2.1-jre'
    implementation 'com.squareup.okhttp3:okhttp:5.0.0-alpha.14'
    implementation 'org.osmdroid:osmdroid-android:6.1.18'
    testImplementation 'org.junit.jupiter:junit-jupiter:5.10.3'
    testImplementation 'org.testng:testng:7.10.2'
}